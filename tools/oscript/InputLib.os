#Использовать cmdline
#Использовать logos
#Использовать tempfiles

Перем Лог;
Перем ИсходнаяОбработка;
Перем ПодключаемыеБиблиотеки;
Перем КаталогНовойОбработки;
Перем РабочийКаталог;

Перем КодВозврата;

Процедура Инициализация()
	Лог = Логирование.ПолучитьЛог("oscript.app.MakeMagic");
	Лог.Закрыть();

	ВыводПоУмолчанию = Новый ВыводЛогаВКонсоль();
	Лог.ДобавитьСпособВывода(ВыводПоУмолчанию);
КонецПроцедуры

Функция ПрочитатьАргументыКоманднойСтроки()
	Если АргументыКоманднойСтроки.Количество() <> 3 Тогда
		Лог.Ошибка("Переданы некорректные параметры командной строки! Параметров должно быть 3");
		Возврат Ложь;
	КонецЕсли;
	
	ИсходнаяОбработка		= АргументыКоманднойСтроки[0];
	ПодключаемыеБиблиотеки	= АргументыКоманднойСтроки[1];
	КаталогНовойОбработки	= АргументыКоманднойСтроки[2];

	//Здесь могут быть проверки аргументов

	Возврат Истина;
КонецФункции

Функция РазобратьВнешнююОбработку()
	Файл = Новый Файл(ИсходнаяОбработка);
	КопироватьФайл(ИсходнаяОбработка, ОбъединитьПути(РабочийКаталог, Файл.Имя));
	ЗапуститьПриложение("precommit1c --decompile ./" + Файл.Имя, РабочийКаталог, Истина, КодВозврата);
	Возврат (КодВозврата = 0);
КонецФункции

Функция СобратьВнешнююОбработку()
    Файл = Новый Файл(ИсходнаяОбработка);
    ЗапуститьПриложение("precommit1c --compile ./src/" + Файл.Имя, РабочийКаталог, Истина, КодВозврата);
	КопироватьФайл(ОбъединитьПути(РабочийКаталог, Файл.Имя), ОбъединитьПути(КаталогНовойОбработки, Файл.Имя));
	Возврат (КодВозврата = 0);
КонецФункции

Функция ДополнитьФайлМодуляОбъекта()
	Файл = Новый Файл(ИсходнаяОбработка);

	
	ПутьКМодулюОбъекта = ОбъединитьПути(РабочийКаталог, Файл.ИмяБезРасширения, "ObjectModule.bsl");
	ФайлМодуля = Новый Файл(ПутьКМодулюОбъекта);
	ТекстМодуля = "";
	Если ФайлМодуля.Существует() Тогда
		Лог.Информация("Модуль объекта уже существует");
		Чтение = Новый ЧтениеТекста(ПутьКМодулюОбъекта, КодировкаТекста.UTF8);
		ТекстМодуля = Чтение.Прочитать();
		Чтение.Закрыть();
	Иначе
		Лог.Информация("Модуль объекта нет");
	КонецЕсли;

	ТекстМодуля = ТекстМодуля + Символы.ПС + Символы.ВК + "//ТЕКСТ ДАЛЬШЕ СГЕНЕРИРОВАН АВТОМАТИЧЕСКИ";
	Лог.Информация(СтрЗаменить(ПодключаемыеБиблиотеки, ".", ТекущийКаталог()));
	//Читаем файлы из библиотеки
	МассивБиблиотек = НайтиФайлы(СтрЗаменить(ПодключаемыеБиблиотеки, ".", ТекущийКаталог()), 
								"ObjectModule.bsl", 
								Истина);
	Для Каждого ФайлБиблиотеки из МассивБиблиотек Цикл
		Лог.Информация(ФайлБиблиотеки.ПолноеИмя);
		ФайлКаталог = Новый Файл(ФайлБиблиотеки.Путь);
		Чтение = Новый ЧтениеТекста(ФайлБиблиотеки.ПолноеИмя, КодировкаТекста.UTF8);
		ТекстБиблиотеки = Чтение.Прочитать();
		ТекстМодуля = ТекстМодуля + Символы.ПС + Символы.ВК + 
		"//--------------------------------------------
		|// " + ВРег(ФайлКаталог.Имя) + "
		|//--------------------------------------------";
		ТекстМодуля = ТекстМодуля + Символы.ПС + Символы.ВК + ТекстБиблиотеки;
	КонецЦикла;
	Запись = Новый ЗаписьТекста;
	Запись.Открыть(ПутьКМодулюОбъекта, КодировкаТекста.UTF8);
	Запись.Записать(ТекстМодуля);
	Запись.Закрыть();
	Возврат Истина;
КонецФункции


КодВозврата = 0;
Инициализация();

Если НЕ ПрочитатьАргументыКоманднойСтроки() Тогда
	ЗавершитьРаботу(КодВозврата);
КонецЕсли;

РабочийКаталог = ВременныеФайлы.СоздатьКаталог();
Лог.Информация(РабочийКаталог);
Если НЕ РазобратьВнешнююОбработку() ИЛИ
	НЕ ДополнитьФайлМодуляОбъекта() ИЛИ
	НЕ СобратьВнешнююОбработку() Тогда
	ЗавершитьРаботу(КодВозврата);
КонецЕсли;

//ВременныеФайлы.Удалить();




